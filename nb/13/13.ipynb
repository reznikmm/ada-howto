{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This solution for Part II requires 128 bits integers to implement `(A * B mod N)`, where `A`, `B` and `N` are 64-bits integers. It works on GCC 11 or GNAT Pro 21. Both of them are not awailable for Jupyter notebook for now. It works only for prime numbers for `bus id`.\n",
    "For datails you may look at \n",
    "[The Art of Computer Programming](https://en.wikipedia.org/wiki/The_Art_of_Computer_Programming#Volume_2_%E2%80%93_Seminumerical_Algorithms) 4.3.2. Modular Arithmetic and\n",
    "[Euler's totient function](https://en.wikipedia.org/wiki/Euler%27s_totient_function)\n",
    "for a prime power argument.\n",
    "\n",
    "\n",
    "```ada\n",
    "with Ada.Text_IO;\n",
    "with Ada.Long_Integer_Text_IO;\n",
    "with Ada.Strings.Fixed;\n",
    "with Ada.Strings.Maps;\n",
    "with Ada.Containers.Vectors;\n",
    "\n",
    "procedure Main is\n",
    "\n",
    "   type Bus is record\n",
    "      Id     : Long_Integer;\n",
    "      Offset : Long_Integer;\n",
    "   end record;\n",
    "\n",
    "   package Bus_Vectors is new Ada.Containers.Vectors (Positive, Bus);\n",
    "\n",
    "   Comma : Ada.Strings.Maps.Character_Set :=\n",
    "     Ada.Strings.Maps.To_Set (\",\");\n",
    "   Input     : Ada.Text_IO.File_Type;\n",
    "   Vector    : Bus_Vectors.Vector;\n",
    "begin\n",
    "   Ada.Text_IO.Open (Input, Ada.Text_IO.In_File, \"/home/jovyan/md/13/input\");\n",
    "\n",
    "   declare\n",
    "      Ignore : String := Ada.Text_IO.Get_Line (Input);\n",
    "   begin\n",
    "      null;\n",
    "   end;\n",
    "\n",
    "   declare\n",
    "      Offset  : Long_Integer := 0;\n",
    "      Schedule  : String := Ada.Text_IO.Get_Line (Input);\n",
    "      First, Last : Natural := 0;\n",
    "   begin\n",
    "      while Last < Schedule'Last loop\n",
    "         Ada.Strings.Fixed.Find_Token\n",
    "           (Source => Schedule,\n",
    "            Set    => Comma,\n",
    "            From   => Last + 1,\n",
    "            Test   => Ada.Strings.Outside,\n",
    "            First  => First,\n",
    "            Last   => Last);\n",
    "\n",
    "         exit when First > Last;\n",
    "\n",
    "         if Schedule (First .. Last) /= \"x\" then\n",
    "            declare\n",
    "               Bus_Id : Long_Integer;\n",
    "            begin\n",
    "               Ada.Long_Integer_Text_IO.Get\n",
    "                 (From => Schedule (First .. Last),\n",
    "                  Item => Bus_Id,\n",
    "                  Last => First);\n",
    "               Vector.Append ((Bus_Id, Offset));\n",
    "            end;\n",
    "         end if;\n",
    "         Offset := Offset + 1;\n",
    "      end loop;\n",
    "   end;\n",
    "\n",
    "   declare\n",
    "      function Power_Mod (M, D, N : Long_Integer) return Long_Integer is\n",
    "      --  A function to calculate (M ** D) mod N;\n",
    "\n",
    "         function Is_Odd (X : Long_Integer) return Boolean is\n",
    "           (X mod 2 /= 0);\n",
    "\n",
    "         function Mult_Mod (A, B, N : Long_Integer) return Long_Integer is\n",
    "            --  A function to calculate (M * D) mod N;\n",
    "            type Int_128 is mod 2**128;\n",
    "            AX : Int_128 := Int_128 (A);\n",
    "            BX : Int_128 := Int_128 (B);\n",
    "            NX : Int_128 := Int_128 (N);\n",
    "         begin\n",
    "            return Long_Integer ((AX * BX) mod NX);\n",
    "         end Mult_Mod;\n",
    "\n",
    "         Result : Long_Integer := 1;\n",
    "         Exp    : Long_Integer := D;\n",
    "         Mult   : Long_Integer := M mod N;\n",
    "      begin\n",
    "         while Exp /= 0 loop\n",
    "            --  Loop invariant is Power_Mod'Result = Result * Mult**Exp mod N\n",
    "            if Is_Odd (Exp) then\n",
    "               Result := Mult_Mod (Result, Mult, N);\n",
    "            end if;\n",
    "\n",
    "            Mult := Mult_Mod (Mult, Mult, N);\n",
    "            Exp := Exp / 2;\n",
    "         end loop;\n",
    "\n",
    "         return Result;\n",
    "      end Power_Mod;\n",
    "\n",
    "      M : Long_Integer := 1;\n",
    "      S : Long_Integer := 0;\n",
    "   begin\n",
    "      for X of Vector loop\n",
    "         M := M * X.Id;\n",
    "      end loop;\n",
    "\n",
    "      for X of Vector loop\n",
    "         if X.Offset /= 0 then\n",
    "            S := S + (X.Id - X.Offset) * Power_Mod (M / X.Id, X.Id - 1, M);\n",
    "            S := S mod M;\n",
    "         end if;\n",
    "      end loop;\n",
    "\n",
    "      Ada.Long_Integer_Text_IO.Put (S);\n",
    "   end;\n",
    "end Main;\n",
    "\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "----\n",
    "[Back to Table of Contents](https://github.com/reznikmm/ada-howto/tree/advent-2020)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Ada",
   "language": "ada",
   "name": "ada"
  },
  "language_info": {
   "codemirror_mode": "ada",
   "file_extension": ".adb",
   "mimetype": "text/x-ada",
   "name": "Ada",
   "pygments_lexer": "ada",
   "version": "2012"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
