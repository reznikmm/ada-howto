[![Binder](https://mybinder.org/badge_logo.svg)](https://mybinder.org/v2/gh/reznikmm/ada-howto/advent-2020?filepath=%2Fhome%2Fjovyan%2Fnb%2F05%2F05.ipynb)

# Day 5

Firstly, we need some standard packages to read the text file and process strings.


```Ada
with Ada.Text_IO;
with Ada.Strings.Fixed;
with Ada.Strings.Maps;
```

Seat specification corresponds to a binary number where `F` and `L` represent `0`, while `B` and `R` represent `1`. So we just translate letters to (`1` or `0`) and then cast this image to a number with base `2`. To do this we need a mapping:


```Ada
Map : constant Ada.Strings.Maps.Character_Mapping :=
  Ada.Strings.Maps.To_Mapping
    (From => "FLBR",
     To   => "0011");
```

We keep maximum seat number in `Result`. And declare `Input` for a text file.


```Ada
Result : Natural := 0;
Input  : Ada.Text_IO.File_Type;
```

Also we will save occuped seats in `Seats` array to use it in part 2. We know that seat number is 10 bits in length, so maximum seat number is less then 2ยนโฐ.


```Ada
Seats  : array (0 .. 2**10 - 1) of Boolean := (others => False);
```

In Ada binary numbers are written in form of `2#10100101000#`. Now we are ready to solve part 1:


```Ada
Ada.Text_IO.Open (Input, Ada.Text_IO.In_File, "/home/jovyan/md/05/input");

while not Ada.Text_IO.End_Of_File (Input) loop
   declare
      Seat_Code : String (1 .. 10) := Ada.Text_IO.Get_Line (Input);
      Image     : String :=
        "2#" & Ada.Strings.Fixed.Translate (Seat_Code, Map) & "#";
      Value     : Natural := Natural'Value (Image);
   begin
      Seats (Value) := True;
      Result := Natural'Max (Result, Value);
   end;
end loop;
   
Ada.Text_IO.Put_Line (Result'Image);
```




     813




Having occuped seats marked as `True` in the `Seats`, to solve part 2 we just search for `True, False, True` pattern in it.


```Ada
declare
   State : Boolean := False;
begin
   for J in Seats'First + 1 .. Seats'Last - 1 loop
      if Seats (J - 1 .. J + 1) = (True, False, True) then
         Ada.Text_IO.Put_Line (J'Image);
      end if;
   end loop;
end;
```




     612




----
[Back to Table of Contents](https://github.com/reznikmm/ada-howto/tree/advent-2020)
