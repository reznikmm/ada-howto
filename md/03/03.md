[![Binder](https://mybinder.org/badge_logo.svg)](https://mybinder.org/v2/gh/reznikmm/ada-howto/advent-2020?filepath=%2Fhome%2Fjovyan%2Fnb%2F03%2F03.ipynb)

# Day 3

Firstly, we need standard packages to read the text file.


```Ada
with Ada.Text_IO;
```

Let's declare Toboggan as a type, because we need several of them to descent in the second part. The toboggan is configured by a slope. The slope is DX/DY rational number. Besides this the toboggan will save its current position as Offset_X, Offset_Y. While Offset_X changes from 0 till map width, the Offset_Y changes from 0 till DY - 1, because we process map in rows going in Y direction. We also keep tree count.


```Ada
type Toboggan (DX, DY : Positive := 1) is record
   Offset_X : Natural := 0;
   Offset_Y : Natural := 0;
   Trees    : Natural := 0;
end record;
```

Now lets having one row of the map we change the tobbagan state:


```Ada
procedure Step_Y
  (Self : in out Toboggan;
   Map  : String) is
begin
   if Self.Offset_Y = 0 and Map (Map'First + Self.Offset_X) = '#' then
      Self.Trees := Self.Trees + 1;
   end if;
      
   Self.Offset_Y := Self.Offset_Y + 1;
     
   if Self.Offset_Y = Self.DY then
      Self.Offset_Y := 0;
      Self.Offset_X := (Self.Offset_X + Self.DX) mod Map'Length;
   end if;
end Step_Y;
```

Rest part is simple. Let's configure 5 toboggans:


```Ada
Toboggans : array (Positive range <>) of Toboggan :=
  (1 => (DX => 1, DY => 1, others => <>),
   2 => (DX => 3, DY => 1, others => <>),
   3 => (DX => 5, DY => 1, others => <>),
   4 => (DX => 7, DY => 1, others => <>),
   5 => (DX => 1, DY => 2, others => <>));
```

And run them all over the map as we read it:


```Ada
declare
   Input : Ada.Text_IO.File_Type;
begin
   Ada.Text_IO.Open (Input, Ada.Text_IO.In_File, "/home/jovyan/md/03/input");

   while not Ada.Text_IO.End_Of_File (Input) loop
      declare
         Line : String := Ada.Text_IO.Get_Line (Input);
      begin
         for T of Toboggans loop
            Step_Y (T, Line);
         end loop;
      end;
   end loop;
end;
```

Now we print tree counts of each toboggan and a final result:


```Ada
declare
   Result : Positive := 1;
begin
   for T of Toboggans loop
      Ada.Text_IO.Put_Line (T.Trees'Image);
      Result := Result * T.Trees;
   end loop;
      
   Ada.Text_IO.Put_Line (Result'Image);
end;
```




     61
     257
     64
     47
     37
     1744787392



