[![Binder](https://mybinder.org/badge_logo.svg)](https://mybinder.org/v2/gh/reznikmm/ada-howto/advent-2020?filepath=%2Fhome%2Fjovyan%2Fnb%2F18%2F18.ipynb)

# Day 18
We need a standard package to read the input.


```Ada
with Ada.Text_IO;
```

A function to parse and evaluate one expression. It parses string according to this grammar rules:

    exp ::= add { * add }
    add ::= item { + item }
    item := '(' exp ')' | number



```Ada
function Evaluate (Line : String) return Long_Integer;

function Evaluate (Line : String) return Long_Integer is
   --  exp ::= add { * add }
   --  add ::= item { + item }
   --  item := '(' exp ')' | number
   procedure Skip_Spaces (Index : in out Positive) is
   begin
      while Index <= Line'Last and then Line (Index) = ' ' loop
         Index := Index + 1;
      end loop;
   end Skip_Spaces;

   procedure Parse_Item
     (Index : in out Positive; Result : out Long_Integer);

   procedure Parse_Add
     (Index : in out Positive; Result : out Long_Integer)
   is
      Temp : Long_Integer;
   begin
      Parse_Item (Index, Result);
      while Index <= Line'Last and then Line (Index) = '+' loop
         Index := Index + 1;
         Parse_Item (Index, Temp);
         Result := Result + Temp;
      end loop;
   end Parse_Add;

   procedure Parse_Exp
     (Index : in out Positive; Result : out Long_Integer)
   is
      Temp : Long_Integer;
   begin
      Parse_Add (Index, Result);
      while Index <= Line'Last and then Line (Index) = '*' loop
         Index := Index + 1;
         Parse_Add (Index, Temp);
         Result := Result * Temp;
      end loop;
   end Parse_Exp;

   procedure Parse_Item
     (Index : in out Positive; Result : out Long_Integer) is
   begin
      Skip_Spaces (Index);

      if Line (Index) = '(' then
         Index := Index + 1;
         Parse_Exp (Index, Result);
         pragma Assert (Line (Index) = ')');
      else
         Result := Long_Integer'Value (Line (Index .. Index));
      end if;

      Index := Index + 1;
      Skip_Spaces (Index);
   end Parse_Item;

   Index : Positive := Line'First;
   Result : Long_Integer;
begin
   Parse_Exp (Index, Result);
   pragma Assert (Index > Line'Last);
   return Result;
end Evaluate;
```

Define input and result variables


```Ada
Input : Ada.Text_IO.File_Type;
Result : Long_Integer := 0;
```

Now just loop over input lines


```Ada
Ada.Text_IO.Open (Input, Ada.Text_IO.In_File, "/home/jovyan/md/18/input");

while not Ada.Text_IO.End_Of_Line (Input) loop
   declare
      Line : constant String := Ada.Text_IO.Get_Line (Input);
   begin
      Result := Result + Evaluate (Line);
   end;
end loop;

Ada.Text_IO.Put_Line (Result'Image);
```




     328920644404583




----
[Back to Table of Contents](https://github.com/reznikmm/ada-howto/tree/advent-2020) 
