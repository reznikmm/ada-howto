[![Binder](https://mybinder.org/badge_logo.svg)](https://mybinder.org/v2/gh/reznikmm/ada-howto/advent-2020?filepath=%2Fhome%2Fjovyan%2Fnb%2F01%2F01.ipynb)

# Day 01

Firstly, we need standard packages to read the text file. We also use a hashed set to store input values.


```Ada
with Ada.Text_IO;
with Ada.Integer_Text_IO;
with Ada.Containers.Hashed_Sets;
```

Now declare an input file object to read it latter.


```Ada
Input : Ada.Text_IO.File_Type;
```

Let's make a Positive_Sets as a hashed set instance.


```Ada
function Hash (Value : Positive) return Ada.Containers.Hash_Type is
  (Ada.Containers.Hash_Type'Mod (Value));

package Positive_Sets is new Ada.Containers.Hashed_Sets
  (Element_Type        => Positive,
   Hash                => Hash,
   Equivalent_Elements => "=");

Values : Positive_Sets.Set;
```

Now we read all input into the `Values` set.


```Ada
Ada.Text_IO.Open (Input, Ada.Text_IO.In_File, "/home/jovyan/md/01/input");

while not Ada.Text_IO.End_Of_File (Input) loop
   declare
      Value : Positive;
   begin
      Ada.Integer_Text_IO.Get (Input, Value);
      Values.Insert (Value);
   end;
end loop;
```

Now we iterates overvalues and check if we also have (2020 - X) in Values:


```Ada
for X of Values loop
   if Values.Contains (2020 - X) then
      Ada.Integer_Text_IO.Put (X * (2020 - X));
      exit;
   end if;
end loop;
```




         197451



## Part 2

Now we append one more nesting loop.


```Ada
Loop_X:
for X of Values loop
   for Y of Values loop
      declare
         Z : constant Integer := 2020 - X - Y;
      begin
         if Z > 0 and then Values.Contains (Z) then
            Ada.Integer_Text_IO.Put (X * Y * Z);
            exit Loop_X;
         end if;
      end;
   end loop;
end loop Loop_X;
```




      138233720



As [John Perry](https://github.com/johnperry-math) noted, this won't work if the data appears more than once. In this case `Values.Insert (Value);` will fail.
