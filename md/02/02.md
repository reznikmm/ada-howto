[![Binder](https://mybinder.org/badge_logo.svg)](https://mybinder.org/v2/gh/reznikmm/ada-howto/advent-2020?filepath=%2Fhome%2Fjovyan%2Fnb%2F02%2F02.ipynb)

# Day 02

Firstly, we need standard packages to read the text file.


```Ada
with Ada.Text_IO;
with Ada.Integer_Text_IO;
```

Now declare an input file object to read it latter.


```Ada
Input : Ada.Text_IO.File_Type;
```

Let's define password policy as a record type.


```Ada
type Policy is record
   Min_Count, Max_Count : Positive;
   Char : Character;
end record;
```

And a procedure to read the password policy from the Input. We can cheat a bit and read `x-y` as two integers (second is a negative `-y`). Then we read 4 character (` c: `).


```Ada
procedure Read (P : out Policy);
--  Read a password policy.

procedure Read (P : out Policy)
is
   Separator : String (1 .. 4);
   Field     : Integer;
begin
   Ada.Integer_Text_IO.Get (Input, P.Min_Count);
   Ada.Integer_Text_IO.Get (Input, Field);
   pragma Assert (Field < 0);
   P.Max_Count := -Field;
   Ada.Text_IO.Get (Input, Separator);
   P.Char := Separator (2);
end Read;
```

Having a password we can check if it matches the policy. Just count number of matched characters in the password and verify it.


```Ada
function Check (P : Policy; Password : String) return Boolean;
--  Check if the password matches given policy


function Check (P : Policy; Password : String) return Boolean is
   Count : Natural := 0;
begin
   for C of Password loop
      if P.Char = C then
         Count := Count + 1;
      end if;
   end loop;
   
   return Count in P.Min_Count .. P.Max_Count;
end Check;
```

Now we read file line by line and count matched passwords.


```Ada
declare
   Total : Natural := 0;
begin
   Ada.Text_IO.Open (Input, Ada.Text_IO.In_File, "/home/jovyan/md/02/input");
   
   while not Ada.Text_IO.End_Of_File (Input) loop
      declare
         P        : Policy;
         Password : String (1 .. 40);
         Last     : Positive;
      begin
         Read (P);
         Ada.Text_IO.Get_Line (Input, Password, Last);
         
         if Check (P, Password (1 .. Last)) then
            Total := Total + 1;
         end if;
      end;
   end loop;
   
   Ada.Integer_Text_IO.Put (Total);
   Ada.Text_IO.Close (Input);
end;
```




            546



Second part check is even simplier


```Ada
function Check_2 (P : Policy; Password : String) return Boolean is
(P.Max_Count in Password'Range
   and then (Password (P.Min_Count) = P.Char xor Password (P.Max_Count) = P.Char));
```

Counting loop is mostly the same.


```Ada
declare
   Total : Natural := 0;
begin
   Ada.Text_IO.Open (Input, Ada.Text_IO.In_File, "/home/jovyan/md/02/input");
   
   while not Ada.Text_IO.End_Of_File (Input) loop
      declare
         P        : Policy;
         Password : String (1 .. 40);
         Last     : Positive;
      begin
         Read (P);
         Ada.Text_IO.Get_Line (Input, Password, Last);
         
         if Check_2 (P, Password (1 .. Last)) then
            Total := Total + 1;
         end if;
      end;
   end loop;
   
   Ada.Integer_Text_IO.Put (Total);
   Ada.Text_IO.Close (Input);
end;
```




            275


