# Поддержка грядущего стандарта Ада 202х в GNAT Community Edition 2020

Запустить: [![Binder](https://mybinder.org/badge_logo.svg)](https://mybinder.org/v2/gh/reznikmm/ada-howto/ce-2020?filepath=%2Fhome%2Fjovyan%2Fce2020.ipynb "Run me!")

В мае 2020г. вышла новая редакция компилятора Ada -
[GNAT Community Edition 2020](https://blog.adacore.com/gnat-community-2020-is-here
  "GNAT Community 2020 is here!").
В ней, среди прочих изменений и улучшений, реализована значительная часть новых
возможностей
[грядущего стандарта Ады](http://www.ada-auth.org/standards/ada2x.html
"Ada 202x Language Reference Manual")
носящего условное название Ada 202x.

Давайте познакомимся с ними поближе. Благодаря
[Jupyter Ada Kernel](https://github.com/reznikmm/jupyter)
поэкспериментировать с примерами использования этих возможностей можно прямо
в браузере. Для этого откройте данный "блокнот" просто перейдя по ссылке на 
[mybinder](https://mybinder.org/v2/gh/reznikmm/ada-howto/ce-2020?filepath=%2Fhome%2Fjovyan%2Fce2020.ipynb
  "Run me!").
(Предупреждение: загрузка блокнота может занять некоторое время необходимое для
сборки и запуска контейнера).

Если Вы ранее не работали с блокнотами Jupyter - не пугайтесь, это не сложно.
Основная идея в том, что Вы может выполнять ячейки с кодом. Для этого на ячейке
нажмите `Ctrl+Enter` либо воспользуйтесь меню или иконками. В Jupyter Ada Kernel
ячейка может содержать
[спецификатор контекста](http://www.ada-auth.org/standards/rm12_w_tc1/html/RM-10-1-2.html#S0253
  "context clauses"),
[оператор](http://www.ada-auth.org/standards/rm12_w_tc1/html/RM-5-1.html#S0146
  "statements") или
[определение](http://www.ada-auth.org/standards/rm12_w_tc1/html/RM-3-11.html#S0087
  "declarative items"),
либо несколько таких элементов, но одного типа. Если ячуйка собержит тело 
подпрограммы или пакета, то спецификация должна быть в той же ячейке.
Некоторые ячейки зависят от преыдущих, пожтому лучше выполнять ячейки по порядку.

## Содержание
 * [Активация Ada 202x](#%D0%90%D0%BA%D1%82%D0%B8%D0%B2%D0%B0%D1%86%D0%B8%D1%8F-Ada-202x)
 * [Больше выразительности и удобства](#%D0%91%D0%BE%D0%BB%D1%8C%D1%88%D0%B5-%D0%B2%D1%8B%D1%80%D0%B0%D0%B7%D0%B8%D1%82%D0%B5%D0%BB%D1%8C%D0%BD%D0%BE%D1%81%D1%82%D0%B8-%D0%B8-%D1%83%D0%B4%D0%BE%D0%B1%D1%81%D1%82%D0%B2%D0%B0)
   * [Атрибут 'Image для всех типов](#%D0%90%D1%82%D1%80%D0%B8%D0%B1%D1%83%D1%82-'Image-%D0%B4%D0%BB%D1%8F-%D0%B2%D1%81%D0%B5%D1%85-%D1%82%D0%B8%D0%BF%D0%BE%D0%B2)
   * [Переопределение атрибута 'Image](#%D0%9F%D0%B5%D1%80%D0%B5%D0%BE%D0%BF%D1%80%D0%B5%D0%B4%D0%B5%D0%BB%D0%B5%D0%BD%D0%B8%D0%B5-%D0%B0%D1%82%D1%80%D0%B8%D0%B1%D1%83%D1%82%D0%B0-'Image)
   * [Числовые и строковые литералы для пользовательских типов](#%D0%A7%D0%B8%D1%81%D0%BB%D0%BE%D0%B2%D1%8B%D0%B5-%D0%B8-%D1%81%D1%82%D1%80%D0%BE%D0%BA%D0%BE%D0%B2%D1%8B%D0%B5-%D0%BB%D0%B8%D1%82%D0%B5%D1%80%D0%B0%D0%BB%D1%8B-%D0%B4%D0%BB%D1%8F-%D0%BF%D0%BE%D0%BB%D1%8C%D0%B7%D0%BE%D0%B2%D0%B0%D1%82%D0%B5%D0%BB%D1%8C%D1%81%D0%BA%D0%B8%D1%85-%D1%82%D0%B8%D0%BF%D0%BE%D0%B2)
   * [Итеративные ассоциации в агрегатах массивов](#%D0%98%D1%82%D0%B5%D1%80%D0%B0%D1%82%D0%B8%D0%B2%D0%BD%D1%8B%D0%B5-%D0%B0%D1%81%D1%81%D0%BE%D1%86%D0%B8%D0%B0%D1%86%D0%B8%D0%B8-%D0%B2-%D0%B0%D0%B3%D1%80%D0%B5%D0%B3%D0%B0%D1%82%D0%B0%D1%85-%D0%BC%D0%B0%D1%81%D1%81%D0%B8%D0%B2%D0%BE%D0%B2)
   * [Дельта агрегаты](#%D0%94%D0%B5%D0%BB%D1%8C%D1%82%D0%B0-%D0%B0%D0%B3%D1%80%D0%B5%D0%B3%D0%B0%D1%82%D1%8B)
   * [Плюха @](#%D0%9F%D0%BB%D1%8E%D1%85%D0%B0-@)
 * [Исправления](#%D0%98%D1%81%D0%BF%D1%80%D0%B0%D0%B2%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F-%D0%B8-%D0%BF%D1%80%D0%BE%D1%87%D0%B5%D0%B5)
   * [Импорт С-функций с переменным числом аргументов](#%D0%98%D0%BC%D0%BF%D0%BE%D1%80%D1%82-%D0%A1-%D1%84%D1%83%D0%BD%D0%BA%D1%86%D0%B8%D0%B9-%D1%81-%D0%BF%D0%B5%D1%80%D0%B5%D0%BC%D0%B5%D0%BD%D0%BD%D1%8B%D0%BC-%D1%87%D0%B8%D1%81%D0%BB%D0%BE%D0%BC-%D0%B0%D1%80%D0%B3%D1%83%D0%BC%D0%B5%D0%BD%D1%82%D0%BE%D0%B2)


## Активация Ada 202x

Первое, что нужно сделать, это указать компилятору, что Вы используете новый стандарт.
Обычно это делается при помощи ключа компилятора `-gnat2020`.
Указать его можно в коммандной строке, а лучше в проектном файле.
Но в этом блокноте мы будем пользоваться директивой компилятора `pragma Ada_2020;`.
Следующая ячейка должна выполнятся первой, иначе последующие ячейки работать не будут!
(Также нам пригодится пакет `Ada.Text_IO;`.)




```Ada
pragma Ada_2020;
with Ada.Text_IO;
```

## Больше выразительности и удобства

Авторы стандарта стараются сделать язык более выразительным и удобным. К нововведениям этой группы можно отнести литералы пользовательских типов, агрегаты контейнеров, новые виды агрегатов массивов и записей и прочее. Давайте начнем с атрибута `'Image`.

### Атрибут `'Image` для всех типов

В новом стандарте атрибут `'Image` работает для всех типов, а не только для скалярных, как было раньше. (Если Вы пропустили 
"[технические правки 2016](http://www.ada-auth.org/standards/rm12_w_tc1/html/RM-3-5.html#I1681)"
стандарте Ada 2012, то знайте, теперь префиксом атрибута может выступать не только тип,
но и значение. Запись `My_Type'Image (Value)` можно сократить до `Value'Image`).


```Ada
Integer_Array : array (1 .. 3) of Integer := (1, 2, 3);
```


```Ada
Ada.Text_IO.Put_Line (Integer_Array'Image);
```




    
    [ 1,  2,  3]




### Переопределение атрибута `'Image`

Также можно задать подпрограмму для отображения значений своего типа.
Причём авторы компилятора пошли другим путём, чем написано в стандарте.
(Видимо черновик стандарта будет обновлятся, если ARG приймет новый вариант этой функциональности).
Вариант от AdaCore для меня выглядит странно, да и то, что прописали в черновик - не идеал.

Основная идея в том, чтобы определить аспект `Put_Image` для вашего типа используя костыли из предопределённого пакета:


```Ada
with Ada.Strings.Text_Output.Utils;
```


```Ada
package Source_Locations is
   type Source_Location is record
      Line   : Positive;
      Column : Positive;
   end record
     with Put_Image => My_Put_Image;

   procedure My_Put_Image
     (Sink  : in out Ada.Strings.Text_Output.Sink'Class;
      Value : Source_Location);
end Source_Locations;

package body Source_Locations is

   procedure My_Put_Image
     (Sink  : in out Ada.Strings.Text_Output.Sink'Class;
      Value : Source_Location)
   is
      Line   : constant String := Value.Line'Image;
      Column : constant String := Value.Column'Image;
      Result : constant String :=
        Line (2 .. Line'Last) & ':' & Column (2 .. Column'Last);
   begin
       Ada.Strings.Text_Output.Utils.Put_UTF_8 (Sink, Result);
   end My_Put_Image;

end Source_Locations;
```

Создадим теперь объект нашего типа:


```Ada
Line_10 : Source_Locations.Source_Location := (Line => 10, Column => 1);
```

И напечатаем его:


```Ada
Ada.Text_IO.Put_Line ("Text position: " & Line_10'Image);
```




    Text position: 10:1




### Числовые и строковые литералы для пользовательских типов 
    
Появилась возможность использовать литералы языка для своих типов.
Это позволяет избежать явного вызова функции преобразования.

Это удобно, когда вводишь свои строчные и числовые типы. Давайте, для примера, создадим
свой строчный тип на основе `Unbounded_String`. Нам понадобятся следующие пакеты:


```Ada
with Ada.Strings.Unbounded;
with Ada.Strings.UTF_Encoding.Wide_Wide_Strings;
```

Теперь определим сам тип и аспект `String_Literal` для него:


```Ada
type My_String is new Ada.Strings.Unbounded.Unbounded_String
  with String_Literal => From_String;

function From_String (Text : Wide_Wide_String) return My_String is
 (To_Unbounded_String
   (Ada.Strings.UTF_Encoding.Wide_Wide_Strings.Encode
     (Text)));

Five : My_String := "5";

```


```Ada
Ada.Text_IO.Put_Line (To_String (Five));
```




    5




### Итеративные ассоциации в агрегатах массивов

Иногда требуется конструировать такие значения массивов, что элементы заданы, как функция от индекса. Здесь пригодятся агрегаты в новой форме. Ассоциации этих агрегатов нацинаются со слова `for`:


```Ada
type Integer_Matrix is array (1 .. 3, 1 .. 3) of Integer;

X_3x3 : Integer_Matrix :=
 (for K in 1 .. 3 =>
   (for J in 1 .. 3 => (K * 10 + J)));
```


```Ada
Ada.Text_IO.Put_Line (X_3x3'Image);
```




    
    [
     [ 11,  12,  13],
    
     [ 21,  22,  23],
    
     [ 31,  32,  33]]




### Дельта агрегаты

Для агрегатов записей тоже есть улучшения. Если вам нужно изменить лишь некоторые компоненты записи, может пригодится *дельта агрегат*. В нем нужно указать базовое значение и те компоненты для которых вы хотите изменить значение. Это удобно, когда общее количество компонент записи сравнительно велико, ведь вам не нужно перечеслять из все:


```Ada
type Vector_3D is record
   X, Y, Z : Natural;
end record;

Base_Value : constant Vector_3D := (1, 2, 3);

Projection : constant Vector_3D := (Base_Value with delta Z => 0);
```

Проэкция вычисляется по базовому значению. Координата Z устанавливается равной нулю. Напечатаем его:


```Ada
Ada.Text_IO.Put_Line (Projection'Image);
```




    
    (x =>  1,
     y =>  2,
     z =>  0)




### Агрегаты контейнеров

Реализация не попала в GNAT CE 2020. Будет выглядеть, как:
```ada
package Stacks is
   type Stack is private
     with Aggregate => (Empty => Empty_Stack, Add_Unnamed => Push);

   function Empty_Stack return Stack;

   procedure Push (Self : Stack; Value : Integer);
private
   type Integer_Array is array (Positive range <>) of Integer;
   type Stack is record
      Last : Natural := 0;
      Data : Integer_Array (1 .. 10);
   end record;
end Stacks;
                                
package body Stacks is

   function Empty_Stack return Stack is (Last => 0, Data => (others => <>));

   procedure Push (Self : Stack; Value : Integer) is
   begin
      Self.Last := Self.Last + 1;
      Self.Data (Self.Last) := Value;
   end Push;

end Stacks;
                                                             
S : Stacks.Stack := [1, 2, 3];
```

### Плюха @

Вводится сокращение в операторах присваивания для обозначение левой части. Это сделано чтобы избежать дублирование кода и связанных с этим ошибок, когда левая часть очень длинная. В любом месте выражения справа от `:=` можно написать `@`. Вот илюстрация (довольно бессмысленная):


```Ada
declare 
    Text : String (1 .. 10) := (others => ' ');
    J    : Positive := 2;
    K    : Positive := 1;
begin
    Text (10 - 2 * J + K / 3 + (K + J) / 2) := Character'Succ (@);
    --  Here '@' replaces "Text (10 - 2 * J + K / 3 + (K + J) / 2)"
    Ada.Text_IO.Put_Line (">" & Text & "<");
end;
```




    >      !   <




## Исправления и прочее

### Импорт С-функций с переменным числом аргументов

Добавили совые соглашения о передаче параметров `Convention => C_Variadic_*`. Просто `C` использовать нельзя из-за 
[несовместимости ABI](https://stackoverflow.com/questions/35819037/variadic-function-in-ada-c-ada-binding/55305307#55305307):


```Ada
with Interfaces.C;
```


```Ada
procedure Printf (Format : in Interfaces.C.char_array; V : Interfaces.C.int)
  with Import => True, Convention => C_Variadic_2, External_Name => "printf";
```


```Ada
Printf (Interfaces.C.To_C("X=%d"), 12);
```




    X=12


